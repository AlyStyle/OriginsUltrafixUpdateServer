// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Explosion Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.propertyValue : object.delay

private alias object.value0 : object.timer // its this in ANIMAL_WAIT
private alias object.value0 : object.bounceDir // its this in every other state
private alias object.value1 : object.randomizeDir
private alias object.value2 : object.isPocky

private alias 0 : ANIMAL_WAIT
private alias 1 : ANIMAL_FALL
private alias 2 : ANIMAL_BOUNCING_PLACED
private alias 3 : ANIMAL_BOUNCING_FIXED
private alias 4 : ANIMAL_BOUNCING_FOLLOW
private alias 5 : ANIMAL_BOUNCING_FREE

private alias  0x20000 : ANIMAL_XVEL
private alias -0x40000 : ANIMAL_YVEL

// Player Aliases
private alias object.xpos : player.xpos

// Path ID Aliases
private alias 0 : PATH_A


// ========================
// Events
// ========================

event ObjectUpdate
	if object.isPocky == false
		object.timer++
		if object.timer > 7
			object.timer = 0
			
			object.frame++
			if object.frame > 5
				object.type = TypeName[Blank Object]
				object.frame = 0
			end if
		end if
	else
		switch object.state
		case ANIMAL_WAIT
			if object.timer < object.delay
				object.timer++
			else
				object.xvel = ANIMAL_XVEL
				if object.randomizeDir == true
					object.drawOrder = 4

					Rand(checkResult, 32)
					if checkResult < 16
						object.direction = FACING_LEFT
						FlipSign(object.xvel)
					end if
				else
					object.direction = FACING_LEFT
					FlipSign(object.xvel)
				end if

				object.yvel = -0x40000
				object.state++
			end if
			break

		case ANIMAL_FALL
			object.ypos += object.yvel
			object.yvel += 0x3800

			if object.yvel > 0
				ObjectTileCollision(CSIDE_FLOOR, 0, 8, PATH_A)
				if checkResult == true
					object.yvel = ANIMAL_YVEL
					object.state++
				end if
			end if
			break

		case ANIMAL_BOUNCING_FOLLOW
			if player[0].xpos < object.xpos
				object.direction = FACING_LEFT
			else
				object.direction = FACING_RIGHT
			end if
		// Fall through
		case ANIMAL_BOUNCING_PLACED
		case ANIMAL_BOUNCING_FIXED
		case ANIMAL_BOUNCING_FREE
			object.xpos += object.xvel
			object.ypos += object.yvel
			object.yvel += 0x3800
			if object.yvel > 0
				ObjectTileCollision(CSIDE_FLOOR, 0, 8, PATH_A)
				if checkResult == true
					if object.state == ANIMAL_BOUNCING_FIXED
						if object.bounceDir == FACING_LEFT
							FlipSign(object.xvel)
							object.direction ^= FACING_LEFT
						end if

						object.bounceDir ^= FACING_LEFT
					end if

					object.yvel = ANIMAL_YVEL
				end if
			end if

			object.frame = object.animationTimer
			object.frame >>= 1
			object.animationTimer++
			object.animationTimer &= 3
			break
			
		end switch

		if object.state < ANIMAL_BOUNCING_FIXED
			if object.outOfBounds == true
				object.type = TypeName[Blank Object]
			end if
		end if
	end if
end event


event ObjectDraw
	if object.isPocky == false
		DrawSprite(object.frame)
	else
		switch object.state
		case ANIMAL_WAIT
		case ANIMAL_FALL
			DrawSprite(8)
			break

		case ANIMAL_BOUNCING_PLACED
		case ANIMAL_BOUNCING_FIXED
		case ANIMAL_BOUNCING_FOLLOW
		case ANIMAL_BOUNCING_FREE
			if object.yvel < 0
				DrawSpriteFX(6, FX_FLIP, object.xpos, object.ypos)
			else
				DrawSpriteFX(7, FX_FLIP, object.xpos, object.ypos)
			end if
			break
			
		end switch
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items2.gif")

	// Explosion frames
	SpriteFrame(-8, -8, 16, 16, 230, 18)
	SpriteFrame(-16, -16, 32, 32, 83, 66)
	SpriteFrame(-16, -16, 32, 32, 116, 66)
	SpriteFrame(-16, -16, 32, 32, 149, 66)
	SpriteFrame(-16, -16, 32, 32, 182, 66)
	SpriteFrame(-16, -16, 32, 32, 215, 66)
	
	// Pocky
	SpriteFrame(-8, -12, 16, 24, 222, 231)
	SpriteFrame(-8, -11, 16, 23, 239, 232)
	SpriteFrame(-7, -12, 15, 24, 127, 213)
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-16, -16, 32, 32, 149, 66)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
