// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Dust Puff Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.propertyValue 		: object.targetPlayer

// Player Aliases
private alias object.xpos 				: player.xpos
private alias object.ypos 				: player.ypos
private alias object.direction 			: player.direction
private alias object.animation 			: player.animation
private alias object.collisionBottom 	: player.collisionBottom


// ========================
// Events
// ========================

event ObjectUpdate
	// Update object movement
	object.xpos += object.xvel
	object.ypos += object.yvel
	
	object.animationTimer++
end event


event ObjectDraw
	CheckGreater(object.frame, 3)
	temp0 = checkResult
	CheckLower(object.frame, 11)
	temp0 &= checkResult
	if temp0 == true
		// Spindash dust variant

		// Seek into this object's targeted player
		currentPlayer = object.targetPlayer

		// Make the values match
		object.direction = player[currentPlayer].direction
		object.xpos = player[currentPlayer].xpos
		object.iypos = player[currentPlayer].collisionBottom
		object.ypos += player[currentPlayer].ypos

		// Erase this object if the player isn't spindashing
		if player[currentPlayer].animation != ANI_SPINDASH
			object.type = TypeName[Blank Object]
		else
			DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
		end if
	else
		// Dust Puff variant (from skidding and etc.)
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
	end if
	
	switch object.frame
		
		// drop dash dust part 1
		case 11
		case 12
		case 13
		case 14
		
			if object.animationTimer > 0
				object.frame++
				object.animationTimer = 0
			end if
			break
		
		// spindash dust
		case 4
		case 5
		case 6
		case 7
		case 8
		case 9
			
		// drop dash dust part 2
		case 15
		case 16
		case 17
		case 18
			
			if object.animationTimer > 1
				object.frame++
				object.animationTimer = 0
			end if
			break
		
		// spindash dust loop
		case 10
			
			if object.animationTimer > 1
				object.frame = 4
				object.animationTimer = 0
			end if
			break
		
		// drop dash dust part 3
		case 19
		case 20
		case 21
		case 22
			
			if object.animationTimer > 2
				object.frame++
				object.animationTimer = 0
			end if
			break
		
		// skid dust
		case 0
		case 1
		case 2
		
		// drop dash dust part 4
		case 23
		case 24
		case 25
			
			if object.animationTimer > 3
				object.frame++
				object.animationTimer = 0
			end if
			break
		
		// skid dust end
		case 3
		
		// drop dash dust end
		case 26
			
			if object.animationTimer > 3
				object.type = TypeName[Blank Object]
			end if
			break
			
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Dust.gif")

	// Skid dust frames
	SpriteFrame(-7, -11, 13, 14, 51, 183)		// 0
	SpriteFrame(-7, -11, 13, 14, 65, 183)		// 1
	SpriteFrame(-6, -11, 13, 14, 79, 183)		// 2
	SpriteFrame(-7, -11, 13, 14, 93, 183)		// 3
	
	// Spindash dust frames
	SpriteFrame(-32, -10, 32, 10, 58, 245)		// 4
	SpriteFrame(-32, -12, 32, 12, 91, 243)		// 5
	SpriteFrame(-32, -16, 32, 16, 124, 239)		// 6
	SpriteFrame(-32, -18, 32, 18, 157, 237)		// 7
	SpriteFrame(-32, -20, 32, 20, 190, 235)		// 8
	SpriteFrame(-32, -21, 32, 21, 223, 206)		// 9
	SpriteFrame(-32, -23, 32, 23, 223, 228)		// 10
	
	// Drop dash dust frames
	SpriteFrame(-15, -24, 32, 25, 1, 1)			// 11
	SpriteFrame(-15, -24, 32, 25, 34, 1)		// 12
	SpriteFrame(-15, -24, 32, 25, 67, 1)		// 13
	SpriteFrame(-15, -24, 32, 25, 100, 1)		// 14
	SpriteFrame(-15, -24, 32, 25, 133, 1)		// 15
	SpriteFrame(-15, -24, 32, 25, 166, 1)		// 16
	SpriteFrame(-15, -24, 32, 25, 199, 1)		// 17
	SpriteFrame(-15, -24, 32, 25, 1, 27)		// 18
	SpriteFrame(-15, -24, 32, 25, 34, 27)		// 19
	SpriteFrame(-15, -24, 32, 25, 67, 27)		// 20
	SpriteFrame(-15, -24, 32, 25, 100, 27)		// 21
	SpriteFrame(-15, -24, 32, 25, 133, 27)		// 22
	SpriteFrame(-15, -24, 32, 25, 166, 27)		// 23
	SpriteFrame(-15, -24, 32, 25, 199, 27)		// 24
	SpriteFrame(-15, -24, 32, 25, 1, 53)		// 25
	SpriteFrame(-15, -24, 32, 25, 34, 53)		// 26
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-7, -11, 13, 14, 51, 183)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
