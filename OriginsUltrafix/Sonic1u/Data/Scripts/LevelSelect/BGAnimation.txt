// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: BG Animation Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.bgTimer

// Priority
public alias 0 : PRIORITY_BOUNDS
public alias 1 : PRIORITY_ACTIVE
public alias 2 : PRIORITY_ALWAYS
public alias 3 : PRIORITY_XBOUNDS
public alias 4 : PRIORITY_XBOUNDS_DESTROY
public alias 5 : PRIORITY_INACTIVE
public alias 6 : PRIORITY_BOUNDS_SMALL
public alias 7 : PRIORITY_ACTIVE_SMALL

// Ink Effects
public alias 0 : INK_NONE
public alias 1 : INK_BLEND
public alias 2 : INK_ALPHA
public alias 3 : INK_ADD
public alias 4 : INK_SUB

// Flip Directions
public alias 0 : FLIP_NONE
public alias 1 : FLIP_X
public alias 2 : FLIP_Y
public alias 3 : FLIP_XY

// Player List Pos Aliases
private alias 0 : PLAYER_SONIC_A
private alias 1 : PLAYER_TAILS_A
private alias 2 : PLAYER_KNUCKLES_A
private alias 3 : PLAYER_SONIC_TAILS_A
private alias 4 : PLAYER_KNUCKLES_TAILS_A
private alias 5 : PLAYER_AMY_A
private alias 6 : PLAYER_AMY_TAILS_A

// Player IDs
private alias 0 : CONFIGSCREEN_PLAYER_S
private alias 1 : CONFIGSCREEN_PLAYER_T
private alias 2 : CONFIGSCREEN_PLAYER_K
// Origins Plus Player IDs
private alias 3 : CONFIGSCREEN_PLAYER_A

private alias 4 : CONFIGSCREEN_PLAYER_ST

private alias 5 : CONFIGSCREEN_PLAYER_KT

// Origins Plus Player IDs
//private alias 5 : CONFIGSCREEN_PLAYER_KT_O
private alias 6 : CONFIGSCREEN_PLAYER_AT


// ========================
// Static Values
// ========================

public value BGAnimation_currentPreview = 0
public value object.playerID = 0

// ========================
// Function Defintions
// ========================

// ========================
// Events
// ========================

event ObjectUpdate
	object.bgTimer++
	object.bgTimer &= 0x1FF
	Sin(temp0, object.bgTimer)
	
	// The background is all chunks and tiles in the level so "animating" it is actually just moving the camera around
	if temp0 > 0
		if temp0 < 32
			// Second frame
			screen.yoffset = 256
		else
			// First frame
			screen.yoffset = 0
		end if
	else
		if temp0 > -32
			// Third frame
			screen.yoffset = 512
		else
			// Fourth frame
			screen.yoffset = 768
		end if
	end if
end event


event ObjectDraw
	if stage.actNum == 1
		// If in the Level Select scene, then draw the zone preview too
		
		temp0 = screen.xcenter
		temp0 -= 12
		
		// Frame
		DrawSpriteScreenXY(9, temp0, 160)

		// Zone preview
		DrawSpriteScreenXY(BGAnimation_currentPreview, temp0, 160)

		temp0 = screen.xcenter
		temp0 += 88
		temp1 = 149

		switch object.playerID
		case CONFIGSCREEN_PLAYER_ST
			DrawSpriteScreenFX(11, FX_INK, temp0, temp1)
			DrawSpriteScreenFX(10, FX_INK, temp0, temp1)
			break
			
		case CONFIGSCREEN_PLAYER_S
			DrawSpriteScreenFX(11, FX_INK, temp0, temp1)
			break
			
		case CONFIGSCREEN_PLAYER_T
			DrawSpriteScreenFX(12, FX_INK, temp0, temp1)
			break
			
		case CONFIGSCREEN_PLAYER_K
			DrawSpriteScreenFX(13, FX_INK, temp0, temp1)
			break

		case CONFIGSCREEN_PLAYER_KT
			DrawSpriteScreenFX(13, FX_INK, temp0, temp1)
			DrawSpriteScreenFX(10, FX_INK, temp0, temp1)
			break
			
#platform: USE_ORIGINS
		case CONFIGSCREEN_PLAYER_A
			DrawSpriteScreenFX(14, FX_INK, temp0, temp1)
			break
			
		case CONFIGSCREEN_PLAYER_AT
			DrawSpriteScreenFX(14, FX_INK, temp0, temp1)
			DrawSpriteScreenFX(10, FX_INK, temp0, temp1)
			break
#endplatform
		end switch
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LevelSelect/Icons.gif")
	LoadPalette("AnLvlSel.act", 0, 0, 0, 256)
	

	// Set all BG Animation objects to active
	// (There should only ever be one in a scene, though)
	foreach (TypeName[BG Animation], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = PRIORITY_ACTIVE
	next

	// Preview frames
	SpriteFrame(56, -12, 32, 24, 0, 50)   // Sonic vibin'
	SpriteFrame(56, -12, 32, 24, 1, 85)   // GHZ
	SpriteFrame(56, -12, 32, 24, 34, 85)  // MZ
	SpriteFrame(56, -12, 32, 24, 67, 85)  // SYZ
	SpriteFrame(56, -12, 32, 24, 100, 85) // LZ
	SpriteFrame(56, -12, 32, 24, 133, 85) // SLZ
	SpriteFrame(56, -12, 32, 24, 166, 85) // SBZ
	SpriteFrame(56, -12, 32, 24, 199, 85) // FZ
	SpriteFrame(56, -12, 32, 24, 180, 60) // Special Stage
	
	// Frame
	SpriteFrame(32, -24, 80, 50, 0, 0)

	SpriteFrame(36, 0, 16, 23, 63, 386)		// Partner Tails 		- #10
	SpriteFrame(16, 0, 16, 23, 46, 386)		// Sonic			- #11
	SpriteFrame(16, 0, 16, 23, 63, 386)		// Tails 			- #12
	SpriteFrame(16, 0, 16, 23, 80, 386)		// Knuckles			- #13
#platform: USE_ORIGINS
	SpriteFrame(14, 1, 17, 22, 83, 410)		// Amy (Alone) 			- #14
#endplatform

	// Default to the EHZ icon
	BGAnimation_currentPreview = 1
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
end event


event RSDKDraw
	DrawSprite(1)
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("LevelSelect/Icons.gif")
	SpriteFrame(56, -12, 32, 24, 0, 50)
	SpriteFrame(32, -24, 80, 50, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
